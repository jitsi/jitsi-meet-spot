apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

def Properties properties = new Properties()

if (project.rootProject.file('local.properties').canRead()) {
    properties.load(new FileInputStream(project.rootProject.file('local.properties')))

} else {
    throw new GradleException("Could not read local.properties!")
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    api 'androidx.appcompat:appcompat:1.1.0'
    api 'androidx.core:core-ktx:1.2.0'
    api 'androidx.constraintlayout:constraintlayout:1.1.3'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'io.reactivex.rxjava2:rxkotlin:2.4.0'
    api "com.google.dagger:dagger:2.25.4"
    api 'com.github.silvestrpredko:dot-progress-bar:1.1'
    api 'org.altbeacon:android-beacon-library:2.16.4'
    kapt "com.google.dagger:dagger-compiler:2.25.4"
}

publishing {
    publications {
        nexus(MavenPublication) {
            groupId 'com.spot'
            artifactId 'spot-tv'
            version '0.0.7'
            artifact("$buildDir/outputs/aar/spot-release.aar")
        }
    }

    repositories {
        maven {
            url "http://nexus.ro.ucverse.com/nexus/repository/maven-releases/"
            credentials {
                username properties.getProperty('nexus_username')
                password properties.getProperty('nexus_password')
            }
        }
    }
}